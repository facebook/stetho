apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

apply plugin: 'android-unit-test'

dependencies {
    compile 'commons-cli:commons-cli:1.2'
    compile 'com.google.code.findbugs:jsr305:2.0.1'

    testCompile 'junit:junit:4.12'
    testCompile('org.robolectric:robolectric:2.4') {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
    }
    testCompile 'org.powermock:powermock-api-mockito:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4:1.6.1'
}

apply from: rootProject.file('release.gradle')

android.libraryVariants.all { variant ->
    def name = variant.name.capitalize()

    // Ugly kludge to rename license files in the bundled commons-cli
    // dependency so that they do not appear to describe Stetho's license.
    configurations.compile.each {
        if (it.getName() == 'commons-cli-1.2.jar') {
            def depJarPath = it.getPath()
            task "tidyCommonsCli${name}"(type: Copy) {
                from zipTree(depJarPath)
                into "build/commons-cli-tidy-${name}"
                rename 'LICENSE', 'commons-cli-LICENSE'
                rename 'NOTICE', 'commons-cli-NOTICE'
            }
        }
    }

    task "metainf${name}"(type: Copy) {
        from rootProject.file('LICENSE')
        into "build/metainf-${name}/META-INF"
    }

    task "fatjar${name}"(type: Jar, dependsOn: [ "jar${name}", "tidyCommonsCli${name}", "metainf${name}" ]) {
        classifier = 'fatjar'
        from variant.javaCompile.destinationDir
        from "build/commons-cli-tidy-${name}"
        from "build/metainf-${name}"
    }
}
